import me.modmuss50.mpp.ReleaseType
import net.fabricmc.loom.task.RemapJarTask
import org.gradle.jvm.tasks.Jar
import java.util.*

plugins {
    id("dev.architectury.loom")
    id("architectury-plugin")
    id("me.modmuss50.mod-publish-plugin")
    id("com.gradleup.shadow")
}

val minecraft = stonecutter.current.version
val loader = loom.platform.get().name.lowercase()
// 26.2+ is unobfuscated: no Yarn/Mojang mappings are published, so loom has
// nothing to remap and does not register a remapJar task. We compile directly
// against the official-names Minecraft jar (no mappings(...) dependency, no
// access widener) and ship the plain jar as the final artifact. Toggled
// per-version via the fabric.loom.disableObfuscation gradle property
// (see versions/26.2-fabric). Base loom stays in the plugins block so the loom
// extension accessor is generated; loom-specific configurations/tasks that are
// not registered in no-remap mode are referenced by name (tasks.named/add) and
// only used when !noRemap. architectury-plugin 3.5+ tolerates the absent
// remapJar task that older versions hard-required.
val noRemap = stonecutter.eval(minecraft, ">=26.2")

version = "${mod.version}+$minecraft"
group = mod.group
base {
    archivesName.set("${mod.id}-$loader")
}

architectury.common(stonecutter.tree.branches.mapNotNull {
    if (stonecutter.current.project !in it) null
    else it.prop("loom.platform")
})
repositories {
    maven("https://maven.neoforged.net/releases/")

    //modmenu
    maven("https://maven.terraformersmc.com/")
    //placeholder api (modmenu depencency)
    maven("https://maven.nucleoid.xyz/")

    maven("https://api.modrinth.com/maven")
}
dependencies {
    add("minecraft", "com.mojang:minecraft:$minecraft")


    if (stonecutter.eval(minecraft, "<26.2")) {
        add("modCompileOnly", "maven.modrinth:elytra-recast:${mod.dep("elytra_recast")}")
    }

    if (loader == "fabric") {
        if (noRemap) {
            // 26.2 unobfuscated: no mappings, plain implementation/compileOnly (no
            // mod* remap configurations), plain jar is the artifact (no remapJar).
            implementation("net.fabricmc:fabric-loader:${mod.dep("fabric_loader")}")
            compileOnly("com.terraformersmc:modmenu:${mod.dep("modmenu_version")}")
            implementation("net.fabricmc.fabric-api:fabric-api:${mod.dep("fabric_version")}")
        } else {
            add("mappings", "net.fabricmc:yarn:$minecraft+build.${mod.dep("yarn_build")}:v2")
            add("modImplementation", "net.fabricmc:fabric-loader:${mod.dep("fabric_loader")}")
            add("modCompileOnly", "com.terraformersmc:modmenu:${mod.dep("modmenu_version")}")
            //some features (like automatic resource loading from non vanilla namespaces) work only with fabric API installed
            //for example translations from assets/modid/lang/en_us.json won't be working, same stuff with textures
            //but we keep runtime only to not accidentally depend on fabric's api, because it doesn't exist in neo/forge
            add("modImplementation", "net.fabricmc.fabric-api:fabric-api:${mod.dep("fabric_version")}")
        }
    }
    if (loader == "forge") {
        "forge"("net.minecraftforge:forge:${minecraft}-${mod.dep("forge_loader")}")
        add("mappings", "net.fabricmc:yarn:$minecraft+build.${mod.dep("yarn_build")}:v2")

        "io.github.llamalad7:mixinextras-forge:${mod.dep("mixin_extras")}".let {
            implementation(it)
            include(it)
        }
    }
    if (loader == "neoforge") {
        "neoForge"("net.neoforged:neoforge:${mod.dep("neoforge_loader")}")
        add("mappings", loom.layered {
            mappings("net.fabricmc:yarn:$minecraft+build.${mod.dep("yarn_build")}:v2")
            mod.dep("neoforge_patch").takeUnless { it.startsWith('[') }?.let {
                mappings("dev.architectury:yarn-mappings-patch-neoforge:$it")
            }
        })
    }
}

loom {
    if (!noRemap) {
        accessWidenerPath = rootProject.file("src/main/resources/jjelytraswap.accesswidener")
    }

    decompilers {
        get("vineflower").apply { // Adds names to lambdas - useful for mixins
            options.put("mark-corresponding-synthetics", "1")
        }
    }
    if (loader == "forge") {
        forge.mixinConfigs(
            "jjelytraswap-common.mixins.json",
            "jjelytraswap-forge.mixins.json",
        )
    }
}


val localProperties = Properties()
val localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.inputStream())
}
publishMods {
    val modrinthToken = localProperties.getProperty("publish.modrinthToken", "")
    val curseforgeToken = localProperties.getProperty("publish.curseforgeToken", "")


    file = if (noRemap) tasks.jar.get().archiveFile else tasks.named<RemapJarTask>("remapJar").get().archiveFile
    dryRun = modrinthToken == null || curseforgeToken == null

    displayName = "${mod.name} ${loader.replaceFirstChar { it.uppercase() }} ${property("mod.mc_title")}-${mod.version}"
    version = mod.version
    changelog = rootProject.file("CHANGELOG.md").readText()
    type = ReleaseType.STABLE

    modLoaders.add(loader)

    val targets = property("mod.mc_targets").toString().split(' ')
    modrinth {
        projectId = property("publish.modrinth").toString()
        accessToken = modrinthToken
        targets.forEach(minecraftVersions::add)
        if (loader == "fabric") {
            requires("fabric-api")
            optional("modmenu")
        }
    }

    curseforge {
        projectId = property("publish.curseforge").toString()
        accessToken = curseforgeToken.toString()
        targets.forEach(minecraftVersions::add)
        if (loader == "fabric") {
            requires("fabric-api")
            optional("modmenu")
        }
    }
}

java {
    withSourcesJar()
    val java = when {
        stonecutter.eval(minecraft, ">=26.2") -> JavaVersion.VERSION_25
        stonecutter.eval(minecraft, ">=1.20.5") -> JavaVersion.VERSION_21
        else -> JavaVersion.VERSION_17
    }
    targetCompatibility = java
    sourceCompatibility = java
}

val shadowBundle: Configuration by configurations.creating {
    isCanBeConsumed = false
    isCanBeResolved = true
}

tasks.shadowJar {
    configurations = listOf(shadowBundle)
    archiveClassifier = "dev-shadow"
}

if (!noRemap) {
    tasks.named<RemapJarTask>("remapJar") {
        injectAccessWidener = true
        input = tasks.shadowJar.get().archiveFile
        archiveClassifier = null
        dependsOn(tasks.shadowJar)
    }
    tasks.jar {
        archiveClassifier = "dev"
    }
} else {
    // 26.2 no-remap: the plain jar is the final artifact (no remapJar step).
    tasks.jar {
        archiveClassifier = null
    }
}

val buildAndCollect = tasks.register<Copy>("buildAndCollect") {
    group = "versioned"
    description = "Must run through 'chiseledBuild'"
    val mainJar = if (noRemap) tasks.jar.get().archiveFile else tasks.named<RemapJarTask>("remapJar").get().archiveFile
    val sourcesJar = if (noRemap) (tasks.getByName("sourcesJar") as Jar).archiveFile else tasks.named<Jar>("remapSourcesJar").get().archiveFile
    from(mainJar, sourcesJar)
    into(rootProject.layout.buildDirectory.file("libs/${mod.version}/$loader"))
    dependsOn("build")
}

if (stonecutter.current.isActive) {
    rootProject.tasks.register("buildActive") {
        group = "project"
        dependsOn(buildAndCollect)
    }

    rootProject.tasks.register("runActive") {
        group = "project"
        dependsOn(tasks.named("runClient"))
    }
}

tasks.processResources {
    properties(
        listOf("fabric.mod.json"),
        "id" to mod.id,
        "name" to mod.name,
        "version" to mod.version,
        "minecraft" to mod.prop("mc_dep_fabric")
    )
    properties(
        listOf("META-INF/mods.toml", "pack.mcmeta"),
        "id" to mod.id,
        "name" to mod.name,
        "version" to mod.version,
        "minecraft" to mod.prop("mc_dep_forgelike")
    )
    properties(
        listOf("META-INF/neoforge.mods.toml", "pack.mcmeta"),
        "id" to mod.id,
        "name" to mod.name,
        "version" to mod.version,
        "minecraft" to mod.prop("mc_dep_forgelike")
    )
}

tasks.build {
    group = "versioned"
    description = "Must run through 'chiseledBuild'"
}